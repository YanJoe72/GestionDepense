plugins {
  id 'java'
  id 'application'
  id 'org.javamodularity.moduleplugin' version '1.8.12'
  id 'org.openjfx.javafxplugin' version '0.0.13'
  id 'org.beryx.jlink' version '3.0.1'
}

group 'com.example'
version '1.0'

repositories {
  mavenCentral()
}

ext {
  junitVersion = '5.10.2'
}

sourceCompatibility = '21'
targetCompatibility = '21'

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

application {
  mainModule = 'com.example.javafx'
  mainClass = 'com.example.javafx.HelloApplication'
}

javafx {
  version = '21-ea+24'
  modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
  implementation("org.xerial:sqlite-jdbc:3.44.1.0")
  implementation("org.apache.logging.log4j:log4j-core:2.22.1")
  implementation("org.slf4j:slf4j-log4j12:2.0.11")
  implementation 'org.slf4j:slf4j-api:2.0.11'
  implementation("com.fasterxml.jackson.core:jackson-core:2.16.1")
  implementation("com.fasterxml.jackson.core:jackson-databind:2.16.1")

  testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
  testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
useJUnitPlatform()}

jlink {
  imageZip = project.file("${buildDir}/distributions/app-${javafx.platform.classifier}.zip")
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'Gestion dépenses'
  }
  addExtraDependencies('slf4j')
  jpackage {
    if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
      imageOptions += ['--icon', 'src/main/resources/com/example/javafx/assets/icon.icns']
      installerOptions += ['--mac-package-identifier', 'com.example.javafx', '--mac-package-name', 'Gestion Dépenses', "--app-version", "1.0.0", "--type", "dmg", '--mac-app-store']
      installerType = project.findProperty('installerType')
    }
    else if(org.gradle.internal.os.OperatingSystem.current().windows){
      imageOptions +=['--icon','src/main/resources/com/example/javafx/images/icon.ico']
      installerOptions += ['--win-per-user-install','--win-dir-chooser','--win-menu','--win-shortcut']
      installerType = 'exe'
    }
    else if (org.gradle.internal.os.OperatingSystem.current().linux) {
      imageOptions += ['--icon', 'src/main/resources/com/example/javafx/images/icon.png']
      installerOptions += ['--linux-package-type', 'deb']
      installerType = 'deb'
    }
  }
}

jlinkZip {
  group = 'distribution'
}